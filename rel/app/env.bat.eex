if exist "!USERPROFILE!\.livebookdesktop.bat" (
  call "!USERPROFILE!\.livebookdesktop.bat"
)

if "!LIVEBOOK_EPMDLESS!"=="0" goto epmdmore
if "!LIVEBOOK_EPMDLESS!"=="false" goto epmdmore
set ELIXIR_ERL_OPTIONS=!ELIXIR_ERL_OPTIONS! -epmd_module Elixir.Livebook.EPMD -start_epmd false -erl_epmd_port 0

:epmdmore
for /f "skip=1" %%X in ('wmic os get localdatetime') do if not defined TIMESTAMP set TIMESTAMP=%%X

if defined LIVEBOOK_DISTRIBUTION set RELEASE_DISTRIBUTION=!LIVEBOOK_DISTRIBUTION!
if not defined RELEASE_DISTRIBUTION set RELEASE_DISTRIBUTION=sname

if defined LIVEBOOK_NODE set RELEASE_NODE=!LIVEBOOK_NODE!
if not defined RELEASE_NODE set RELEASE_NODE=livebook-app-!TIMESTAMP:~8,6!-!RANDOM!

set RELEASE_MODE=interactive
set MIX_ARCHIVES=!RELEASE_ROOT!\vendor\archives
set MIX_REBAR3=!RELEASE_ROOT!\vendor\rebar3
if not defined LIVEBOOK_SHUTDOWN_ENABLED set LIVEBOOK_SHUTDOWN_ENABLED=true
set LIVEBOOK_DESKTOP=true
if not defined LIVEBOOK_PORT (set LIVEBOOK_PORT=0)
set PATH=!RELEASE_ROOT!\vendor\otp\erts-<%= @release.erts_version%>\bin;!RELEASE_ROOT!\vendor\otp\bin;!RELEASE_ROOT!\vendor\elixir\bin;!PATH!

set cookie_path=!RELEASE_ROOT!\releases\COOKIE
if not exist %cookie_path% (
  :: '| set /p=""' is so that we don't add ' \r\n' to the cookie
  echo | set /p="cookie-!TIMESTAMP:~0,11!-!RANDOM!" > %cookie_path%
)

cd !HOMEDRIVE!!HOMEPATH!
