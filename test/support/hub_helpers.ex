defmodule Livebook.HubHelpers do
  @moduledoc false

  import ExUnit.Assertions
  import Livebook.Factory
  import Phoenix.LiveViewTest

  def create_team_hub(user, node) do
    hub = build_team_hub(user, node)
    Livebook.Hubs.save_hub(hub)
  end

  def build_team_headers(user, node) do
    hub = build_team_hub(user, node)

    headers = [
      {"x-user", to_string(hub.user_id)},
      {"x-org", to_string(hub.org_id)},
      {"x-org-key", to_string(hub.org_key_id)},
      {"x-session-token", hub.session_token}
    ]

    {hub, headers}
  end

  defp build_team_hub(user, node) do
    teams_org = build(:org)
    teams_key = teams_org.teams_key
    key_hash = Livebook.Teams.Org.key_hash(teams_org)

    org = erpc_call(node, :create_org, [])
    org_key = erpc_call(node, :create_org_key, [[org: org, key_hash: key_hash]])
    org_key_pair = erpc_call(node, :create_org_key_pair, [[org: org]])
    token = erpc_call(node, :associate_user_with_org, [user, org])

    build(:team,
      id: "team-#{org.name}",
      hub_name: org.name,
      user_id: user.id,
      org_id: org.id,
      org_key_id: org_key.id,
      org_public_key: org_key_pair.public_key,
      session_token: token,
      teams_key: teams_key
    )
  end

  def assert_sidebar_hub(view, path, name, emoji \\ "ðŸˆ") do
    hubs_html = view |> element("#hubs") |> render()

    assert hubs_html =~ emoji
    assert hubs_html =~ path
    assert hubs_html =~ name
  end

  defp erpc_call(node, fun, args) do
    :erpc.call(node, Hub.Integration, fun, args)
  end
end
